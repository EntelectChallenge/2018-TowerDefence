(in-package :bot)

(defun make-parser-name (class-name-symbol)
  (let ((name (symbol-name class-name-symbol)))
    (intern (concatenate 'string "PARSE-" name))))

(defun make-hash-parser-name (class-name-symbol)
  (let ((name (symbol-name class-name-symbol)))
    (intern (concatenate 'string "PARSE-" name "-FROM-HASH"))))

(defun make-keyword (sym)
  (let ((name (symbol-name sym)))
    (intern name "KEYWORD")))

(defun camel-case (str)
  (let ((parts (cl-ppcre:split "-+" str)))
    (format nil "狺ㄣ镱ㄣ狎疳螋螬磲疸狎＇篝蜷铉汜痖翎扉ㄣ潋疳螋螬┅┅ㄤ彐躅箢犭瀛汜箦篝颟ㄣ飙痧泸搴蝈珏蝈痨徙瀛犰篝⑦┅ㄤ彐躅筱蝈犴轭绛箢犭瀛汜箦篝颟篝蜷铉躔汜箦箢犭瀛汜箦篝颟┅ㄤ彐躅栳蟓沲篝镯脲ㄡ钿ㄣ镱箴ㄣ徜┅ㄤ彐躅磲脲脲箪雉汜箦骖磲疸狎灬礅溽箪雉ㄩㄨ狍沲篝镯脲箪雉ㄣ徜箪雉ㄦ躅汜祆汜箦骖篝蜷铉滹黝汜箦簌礅镬钺礤ㄧ弭箪雉箪雉┅┅┅箪雉螬ㄤ彐躅磲脲轭轸狎箪雉箴邈磲脲脲黠蜾ㄧ弭箪雉箪雉箴邈┅ㄤ彐躅珏舡箪雉溴骟箪雉箴邈螬祜镳骘箪雉箴邈轭箪雉箴邈泔祆邈戾è箪雉ㄧ弭箪雉箪雉箴邈┅ㄩ铋舡狎磲脲轭轸狎箪雉箴邈┅啜箪雉横沣弩箫箪雉洪铋翎蜱轭轸狎绌┅ㄤ彐躅磲脲篝犷溽蜾箪雉ㄩ铋舡狎脲牦镱镡戛啜轭轸狎ㄧ弭栳箬脲牦镱镡戛┅ㄤ彐躅珏舡箪雉豉疱箪雉ㄣ徜潋箪雉┅ㄤ彐躅磲脲镡赍泗箪雉ㄩ铋舡狎脲箪雉牦镱镡戛戾è栳箬疳蝮弪钺礤磲脲栳箬疳蝮弪钺礤ㄧ弭箪雉豉疱箪雉┅┅啜轭轸狎ì栳箬疳蝮弪钺礤ㄧ弭栳箬脲牦镱镡戛┅┅ㄤ彐躅栳蟓镡赍泗泔铙趄蹉麸箪雉ㄡ钿ㄣ镱箴箪雉ㄣ徜潋箪雉┅ㄤ彐磲泸溴骈铄牦镱泔铙趄蹉麸ㄣ灬篌钺礤箪雉汜箦骖戾舄è脲磲脲脲箪雉汜箦骖┅ㄩ铋舡狎珞磲疸狎＇磲脲轭轸狎箪雉螬ㄨ狍璀疳蝮弪钺礤磲脲栳箬疳蝮弪钺礤沆狍蟓钺礤┅ㄡ戾犷潋獒瑚轸璀珏铙眢牦镱镡戛戾è泔铙趄蹉麸颦疳蜥眢ㄡ痧禊＇狃疱钿祜镳骘脲轭脲骘轭轸狎轭轭轸狎珞骘箪雉轭箪雉泔祆邈ㄩㄨ狍镡赍泗泔铙趄蹉麸箪雉磲脲镡赍泗箪雉轭轸狎脲箪雉牦镱镡戛磲脲篝犷溽蜾箪雉轭轸狎脲牦镱镡戛┅┅啜溴骢栳箬疳蝮弪钺礤ì牦镱镡戛ㄨ犷潇弪汜箦ㄣ镱è铛祆牦镱镡戛鲥泗矧┅è泔铙牦镱镡戛ㄣ镥蜚磲疸狎＇栳箬疳蝮弪钺礤牦镱镡戛鲥泗矧┅磲脲轭篝犷沐К沆狍蟓钺礤楞镱篝蝓泗矧疳蜥眢┅┅┅┅ㄤ彐磲泸溴骈铄疳蝮弪ㄣ灬篌钺礤箪雉汜箦骖戾舄è栳箬疳蝮弪钺礤磲脲栳箬疳蝮弪钺礤沆狍蟓钺礤┅疳蝮弪钺礤磲脲疳蝮弪钺礤沆狍蟓钺礤┅ㄡ戾犷潋獒瑚轸璀珏铙眢牦镱镡戛啜痱镧ㄤ彐轭瀛牦镱泔铙趄蹉麸沆狍蟓钺礤箪雉汜箦骖ㄤ彐躅疳蝮弪钺礤牦镱戾è牦镱镡狍镱吼狎箦牦镱┅ì栳箬疳蝮弪钺礤牦镱镡戛┅┅┅ㄤ彐躅珏舡箪雉箪雉箴邈ㄩㄣ镱箴箪雉箴邈ㄣ狎箪雉箴邈箪雉箴邈┅ㄤ彐磲泸溴骈铄溽翎沆狍钺礤箪雉螬戾è箪雉溴骟ㄧ弭箪雉溴骟箪雉螬┅啜溴驺灬篌钺礤ī箪雉溴骟┅ㄤ彐磲泸溴骈铄痫沆钺礤箪雉汜箦骖啜痱镧ㄤ彐轭瀛溽翎沆狍钺礤箪雉螬ㄤ彐轭瀛疳蝮弪钺礤箪雉汜箦骖┅